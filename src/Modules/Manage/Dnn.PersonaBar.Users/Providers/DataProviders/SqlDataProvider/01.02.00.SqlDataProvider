/************************************************************/
/*****              SqlDataProvider                     *****/
/*****                                                  *****/
/*****                                                  *****/
/***** Note: To manually execute this script you must   *****/
/*****       perform a search and replace operation     *****/
/*****       for {databaseOwner} and {objectQualifier}  *****/
/*****                                                  *****/
/************************************************************/

/** CONTENT-7728: Show only authorized users in the main list. **/
/** DNN-9302:     Fix schema issue and optimize code           **/

IF EXISTS (SELECT * FROM sys.procedures WHERE object_id = object_id(N'{databaseOwner}[{objectQualifier}Personabar_GetUsers]'))
	DROP PROCEDURE {databaseOwner}[{objectQualifier}Personabar_GetUsers]
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}Personabar_GetUsers] 
(   @PortalId      Int,
    @SortColumn    nVarChar(32),
    @SortAscending Bit,
    @PageIndex     Int = 0,
    @PageSize      Int = 10
) AS
BEGIN
    SELECT COUNT(*) AS TotalRecords
    FROM  {databaseOwner}[{objectQualifier}vw_Users]
    WHERE UP.PortalId  = @PortalId
     AND IsSuperUser   = 0 
     AND Authorised    = 1 
     AND IsDeleted     = 0;
		
    WITH UsersCTE
    AS (SELECT UserID, 
               Username, 
               DisplayName, 
               Email, 
               CreatedOnDate, 
               IsDeleted, 
               Authorised,
               ROW_NUMBER() OVER ( ORDER BY CASE WHEN @SortColumn = 'Joined'      AND @SortAscending = 1 THEN UserID      END ASC, 
                                            CASE WHEN @SortColumn = 'Joined'      AND @SortAscending = 0 THEN UserID      END DESC,
                                            CASE WHEN @SortColumn = 'Email'       AND @SortAscending = 0 THEN Email       END DESC,
                                            CASE WHEN @SortColumn = 'DisplayName' AND @SortAscending = 1 THEN DisplayName END ASC, 
                                            CASE WHEN @SortColumn = 'DisplayName' AND @SortAscending = 0 THEN DisplayName END DESC) AS RowNumber 
       FROM  {databaseOwner}[{objectQualifier}vw_Users]
       WHERE PortalId     = @PortalId
         AND IsSuperUser  = 0 
         AND Authorised   = 1 
         AND IsDeleted    = 0
	)
    SELECT * 
     FROM  UsersCTE
     WHERE RowNumber BETWEEN (@PageIndex * @PageSize + 1) AND ((@PageIndex + 1) * @PageSize);
END
GO

IF EXISTS (SELECT * FROM sys.procedures WHERE object_id = object_id(N'{databaseOwner}[{objectQualifier}Personabar_GetUsersByUserIds]'))
	DROP PROCEDURE {databaseOwner}{objectQualifier}Personabar_GetUsersByUserIds
GO

CREATE PROCEDURE {databaseOwner}{objectQualifier}Personabar_GetUsersByUserIds 
(	@PortalId INT,
	@UserIds nvarchar(max) -- comma separated list of UserIds
) AS
BEGIN
	SELECT DISTINCT 
            U.UserID, 
            U.Username, 
            U.DisplayName, 
            U.Email, 
            U.CreatedOnDate, 
            U.IsDeleted, 
            U.Authorised
     FROM {databaseOwner}{objectQualifier}vw_Users                         U 
     JOIN {databaseOwner}{objectQualifier}ConvertListToTable(',',@UserIds) I ON U.UserID = I.RowValue
	 WHERE U.PortalId = @PortalId
END
GO

/************************************************************/
/*****              SqlDataProvider                     *****/
/************************************************************/